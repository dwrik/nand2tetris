// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:

    // process inc
    Inc16 (in=regout, out=incval);
    Mux16 (a=regout, b=incval, sel=inc, out=incout);

    // process load
    Mux16 (a=incout, b=in, sel=load, out=loadout);

    // process reset
    Mux16 (a=loadout, b=false, sel=reset, out=resetout);

    // load register only when load or inc or reset
    Or8Way   (in[0]=load, in[1]=inc, in[2]=reset, out=update);
    Register (in=resetout, load=update, out=out, out=regout);
}
